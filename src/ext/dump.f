\ dump.f   - hex/ascii data dump
\ ------------------------------------------------------------------------

  .( loading dump.f ) cr

\ ------------------------------------------------------------------------
\ not used here but is so commonly used it should be defined somewhere

\ maybe it should be caled .h?

: h. ( n1 --- ) base >r hex . r> !> base ;

\ ------------------------------------------------------------------------
\ display n1 in hex with n2 digits

: .#h   ( n1 n2 --- )
  base >r hex
  >r 0 <# r> rep #
  #> type space
  r> !> base ;

\ ------------------------------------------------------------------------

: .8h 8 .#h ;

\ ------------------------------------------------------------------------
\ display address

: .address     ( a1 --- a1 )
  dup .8h ."  | " ;

\ ------------------------------------------------------------------------
\ display char in hex

  <headers

: .hchar    ( c1 --- )
  2 .#h ;

\ ------------------------------------------------------------------------

: dump-hex  ( a1 --- )
  dup 16 bounds
  do
    i c@ .hchar
  loop
  ." |" ;

\ ------------------------------------------------------------------------
\ dispay char in ascii

: .char
  dup $20 < over
  $7e > or
  if
    drop '.'
  then
  emit ;

\ ------------------------------------------------------------------------

: dump-ascii        ( a1 --- )
  space 16 bounds
  do
    i c@ .char
  loop
  ." |" ;

\ ------------------------------------------------------------------------

  headers>

: dump      ( a1 n1 --- )
  cr
  bounds
  do
    i .address
    dump-hex
    dump-ascii cr
  16 +loop ;

\ ------------------------------------------------------------------------

  behead

\ ========================================================================
